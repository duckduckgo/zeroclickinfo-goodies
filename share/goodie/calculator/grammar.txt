:default ::= action => [value] bless => primary
lexeme default = action => [ start, length, value ]
    bless => ::name latm => 1

:start ::= Calculator

Calculator ::= Expression

Expression ::=
       ExprOp bless => primary
    || Term   bless => primary

Term ::=
       Function bless => primary
    || TermOp   bless => primary
    || Factor   bless => primary

TermOp ::=
       Factor (<to the power>) Term bless => exponentiate assoc => right
    || Term   ('*')            Term bless => multiply
    |  Term   (<divided by>)   Term bless => divide
    |  Factor ('!')                 bless => factorial_operator

<to the power> ~ '^' | 'to the power':i | 'to the power of':i

<divided by> ~ '/' | 'divided by':i

ExprOp ::=
      Expression ('+') Expression bless => add
    | Expression ('-') Expression bless => subtract

Factor ::=
       ('(') Expression (')')  bless => paren assoc => group
    || NumTerm                 bless => primary
    || NumTerm ('e':i) NumTerm bless => exp
    || Factor ('squared')      bless => square

NumTerm ::=
       FactoredConstant bless => primary
    || Constant         bless => primary
    |  Number           bless => primary

FactoredConstant ::=
      Number WordConstant   bless => factored_word_constant
    | Number SymbolConstant bless => factored_symbol_constant

Constant ::=
      WordConstant
    | SymbolConstant

Function ::=
      GenUnaryFunction
    | UnaryFunction

UnaryFunction ::=
      ('log_' ) NumTerm Argument bless => logarithm
    | ('log'  ) NumTerm Argument bless => logarithm

# Argument for a unary function.
Argument ::= ('(') Expression (')') bless => primary

Number ::=
      [$] BaseNumber bless => prefix_currency
    |     BaseNumber bless => primary

BaseNumber ::=
      Integer
    | Decimal

Integer ::= integer bless => integer
Decimal ::= decimal bless => decimal
integer ~    '-' digits     | digits
decimal ~    '-' digits '.'   digits | digits '.' digits
           | '.' digits     | digits '.'
           | '-' digits '.'

digits     ~ [\d]+

:discard   ~ whitespace
whitespace ~ [\s]+
