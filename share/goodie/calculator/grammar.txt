:default ::= action => [value] bless => primary
lexeme default = action => [ start, length, value ]
    bless => ::name latm => 1

:start ::= Calculator

Calculator ::= Expression

Expression ::=
       ExprOp bless => primary
    || Term   bless => primary

Term ::=
       Function bless => primary
    || TermOp   bless => primary
    || Factor   bless => primary

TermOp ::=
       Factor ('^') Term              bless => exponentiate assoc => right
    || Factor ('*') Term              bless => multiply
    |  Factor ('/') Term              bless => divide
    |  Factor ('divided') ('by') Term bless => divide

ExprOp ::=
      Term ('+') Expression bless => add
    | Term ('-') Expression bless => subtract

Factor ::=
       ('(') Expression (')')  bless => paren assoc => group
    || NumTerm                 bless => primary
    || Integer ('!')           bless => factorial_operator
    || NumTerm ('e':i) NumTerm bless => exp
    || Factor ('squared')      bless => square

NumTerm ::=
       Number Constant bless => constant_coefficient
    || Constant        bless => primary
    |  Number          bless => primary

Constant ::=
      pi    bless => const_pi
    | euler bless => const_euler
    | dozen bless => const_dozen
    | score bless => const_score

pi    ~ 'pi':i
euler ~ 'e':i
dozen ~ 'dozen':i
score ~ 'score':i

Function ::=
      ('sqrt' )         Argument bless => square_root
    | ('sin'  )         Argument bless => sine
    | ('cos'  )         Argument bless => cosine
    | ('tan'  )         Argument bless => tangent
    | ('csc'  )         Argument bless => cosec
    | ('sec'  )         Argument bless => secant
    | ('cotan')         Argument bless => cotangent
    | ('log_' ) NumTerm Argument bless => logarithm
    | ('log'  ) NumTerm Argument bless => logarithm
    | ('ln'   )         Argument bless => natural_logarithm
    | ('log'  )         Argument bless => natural_logarithm
    | ('fact' )         Argument bless => factorial
    | ('factorial')     Argument bless => factorial
    | ('exp')           Argument bless => exponential

# Argument for a unary function.
Argument ::= ('(') Expression (')') bless => primary

Number ::=
      [$] BaseNumber bless => prefix_currency
    |     BaseNumber bless => primary

BaseNumber ::=
      Integer
    | Decimal

Integer ::= integer bless => integer
Decimal ::= decimal bless => decimal
integer ~    '-' digits     | digits
decimal ~    '-' digits '.'   digits | digits '.' digits
           | '.' digits     | digits '.'
           | '-' digits '.'

digits     ~ [\d]+

:discard   ~ whitespace
whitespace ~ [\s]+
